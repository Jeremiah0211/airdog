<?xml version="1.0" encoding="utf-8"?>
<mx:HBox xmlns:mx="http://www.adobe.com/2006/mxml" paddingLeft="2" paddingTop="2" paddingBottom="2" height="100%" xmlns:view="no.airdog.view.*" horizontalAlign="center" verticalAlign="middle" cornerRadius="0">
	<mx:Script>
        <![CDATA[
        	import mx.events.FlexEvent;
        	import mx.containers.VBox;
        	import mx.core.UIComponent;
        	import no.airdog.model.Hund;
			import no.airdog.services.Components;      
			import no.airdog.controller.*;
			
			import mx.controls.*;
			
			private var _box:HBox;
			
			public function set hund(val:Hund):void
			{
				while (this.numChildren > 0)
				{             
					this.removeChildAt(0);         
				}
				
				_box = leggTilHund(val);
				this.addChild(_box);
			}   
			
			private function visHund(event:Event):void
			{
				var sender:Button = event.target as Button;
				if (sender != null)
				{
					Components.instance.controller.visHund(sender.data.toString());
				}
			}
			
			private function oppdaterStrek(event:Event):void
			{
				this.invalidateDisplayList();
			}
			
			private function leggTilHund(hund:Hund):HBox
			{
				var hBox:HBox = new HBox();
				var button:Button = new Button();
				var vBox:VBox = new VBox();
				
				hBox.setStyle("verticalAlign","middle")
				hBox.addChild(button);
				hBox.addChild(vBox);
				
				button.name = "hundKnapp";
				button.label = hund.navn;
				button.width = 190;
				button.data = hund.hundId;
				button.addEventListener(MouseEvent.CLICK, visHund);
				button.addEventListener(FlexEvent.UPDATE_COMPLETE, oppdaterStrek);
				
				vBox.name = "foreldreBoks";
				
				if (hund.far != null && hund.far.navn != null)
				{
					vBox.addChild(leggTilHund(hund.far));
				}
				
				if (hund.mor != null && hund.mor.navn != null)
				{
					vBox.addChild(leggTilHund(hund.mor));
				}

				return hBox;
			}
			
			private function tegnStrek(hBox:HBox):void
			{
				var button:Button = hBox.getChildByName("hundKnapp") as Button;
				var buttonRect:Rectangle = button.getRect(this);
				
				var vBox:VBox = hBox.getChildByName("foreldreBoks") as VBox;
				
				if (vBox != null)
				{
					var foreldre:Array = vBox.getChildren();
					
					var i:int;
					for (i = 0; i < foreldre.length; i++) 
					{
    					var nyBox:HBox = foreldre[i] as HBox;
    					tegnStrek(nyBox);
    					
    					var nyBoxRect:Rectangle = nyBox.getRect(this);
    					
    					graphics.moveTo(buttonRect.right, buttonRect.y + (buttonRect.height / 2));
						graphics.lineStyle(3,0xD3D4AA);
						graphics.lineTo(nyBoxRect.x, nyBoxRect.y + (nyBoxRect.height / 2));
					}
				}
			}
			
			override protected function updateDisplayList(unscaledWidth:Number, unscaledHeight:Number):void
			{
				graphics.clear();
				
				super.updateDisplayList(unscaledWidth, unscaledHeight);
				
				if (_box != null)
				{
					_box.invalidateSize();
					_box.invalidateProperties();
					_box.invalidateDisplayList();
					tegnStrek(_box);
				}
			}
        ]]>
    </mx:Script>
</mx:HBox>