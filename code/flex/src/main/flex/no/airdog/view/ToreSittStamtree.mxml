<?xml version="1.0" encoding="utf-8"?>
<mx:HBox xmlns:mx="http://www.adobe.com/2006/mxml" paddingLeft="2" paddingTop="2" paddingBottom="2" height="100%" xmlns:view="no.airdog.view.*" horizontalAlign="center" verticalAlign="middle" cornerRadius="0">
	<mx:Script>
        <![CDATA[
        	import mx.core.UIComponent;
        	import no.airdog.model.Hund;
			import no.airdog.services.Components;      
			import no.airdog.controller.*;
			
			import mx.controls.*;
			
			private var _hundId:String;
			private var _dybde:int;
			
			[Bindable]
			private var hund:Hund;
			
			private var hundMorView:ToreSittStamtree;
			private var hundFarView:ToreSittStamtree;
			
			public function set hundId(val:String):void
			{
				_hundId = val;
				
				while (stamtree != null && stamtree.numChildren > 0)
				{             
					stamtree.removeChildAt(0);         
				}
				
				if (_dybde != 0 && _hundId != null)
				{
					hentHund();
				}
			}   
			
			public function set dybde(val:int):void
			{
				_dybde = val;
				
				if (_hundId != null)
				{
					hentHund();
				}
			}        
			
			private function hentHund():void
			{
				Components.instance.services.airdogService.hentHund(_hundId, hundResultat);
			}
			
			private function hundResultat(event:Object):void
			{
				hund = event as Hund;
				
				if (hund != null)
				{
					if (hund.morId != null)
					{
						//Alert.show("omg " + hund.morId, hund.morId);
						Components.instance.services.airdogService.hentHund(hund.morId, morResultat);
					}
					
					if (hund.farId != null)
					{
						//Alert.show("omg " + hund.farId, hund.farId);
						Components.instance.services.airdogService.hentHund(hund.farId, farResultat);
					}
				}
			}
			
			private function morResultat(event:Object):void
			{
				var hundMor:Hund = event as Hund;
				
				if (hundMor != null && hundMor.navn != null && _dybde != 1)
				{
					hundMorView = new ToreSittStamtree();
					hundMorView.dybde = _dybde - 1;
					hundMorView.hundId = hundMor.hundId;
					
					stamtree.addChildAt(hundMorView, stamtree.numChildren);	
				}
			}
			
			private function farResultat(event:Object):void
			{
				var hundFar:Hund = event as Hund;
				
				if (hundFar != null && hundFar.navn != null && _dybde != 1)
				{
					hundFarView = new ToreSittStamtree();
					hundFarView.dybde = _dybde - 1;
					hundFarView.hundId = hundFar.hundId;
					
					stamtree.addChildAt(hundFarView, 0);
				}
			}
			
			private function visHund(event:Event):void
			{
				Components.instance.controller.visHund(_hundId);
			}
			
			override protected function updateDisplayList(unscaledWidth:Number, unscaledHeight:Number):void
			{
				graphics.clear();
				
				super.updateDisplayList(unscaledWidth, unscaledHeight);
				var toreParent:ToreSittStamtree = this.parentDocument as ToreSittStamtree;
				
				if (hundMorView != null && hundMorView.hundKnapp != null)
				{
					var hundKnappRectangle:Rectangle = hundKnapp.getRect(this);
					var HundTargetRectangle:Rectangle = hundMorView.hundKnapp.getRect(this);
					
					if (HundTargetRectangle.left > 0)
					{
						graphics.moveTo(hundKnappRectangle.left + hundKnappRectangle.width, hundKnappRectangle.top + (hundKnappRectangle.height / 2));
						graphics.lineStyle(3,0xD3D4AA);
						graphics.lineTo(HundTargetRectangle.left, HundTargetRectangle.top + (HundTargetRectangle.height / 2));
					}
				}
				
				if (hundFarView != null && hundFarView.hundKnapp != null)
				{
					var hundKnappRectangle:Rectangle = hundKnapp.getRect(this);
					var HundTargetRectangle:Rectangle = hundFarView.hundKnapp.getRect(this);
					
					if (HundTargetRectangle.left > 0)
					{
						graphics.moveTo(hundKnappRectangle.left + hundKnappRectangle.width, hundKnappRectangle.top + (hundKnappRectangle.height / 2));
						graphics.lineStyle(3,0xD3D4AA);
						graphics.lineTo(HundTargetRectangle.left, HundTargetRectangle.top + (HundTargetRectangle.height / 2));
					}
				}
				
				if (toreParent != null)
				{
					toreParent.invalidateDisplayList();
				}
			}
        ]]>
    </mx:Script>
	<mx:Button id="hundKnapp" label="{hund.navn}" width="190" click="visHund(event)" />
	<mx:VBox id="stamtree">
	</mx:VBox>
</mx:HBox>