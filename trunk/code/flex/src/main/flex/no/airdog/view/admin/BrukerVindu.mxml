<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow
	xmlns:mx="http://www.adobe.com/2006/mxml"
	title="Bruker"
	creationComplete="init()"
	layout="vertical"
	height="100%" width="100%">
	
	<mx:Script>
		<![CDATA[
			import mx.validators.EmailValidator;
			import no.airdog.model.Bruker;
			import no.airdog.controller.*;
			import no.airdog.services.Components;
			import mx.controls.Alert;
			import mx.utils.StringUtil;
			import mx.validators.Validator;
			import mx.events.ValidationResultEvent;
		
			[Bindable]private var erTilbakestillt:Boolean = true;
			[Bindable]public var erGodkjent:Boolean;
			
			public var _aktivBruker:Bruker = null;
			
			private var hvilkeKontroll:DisplayObject;
			
			private var epostValidator:EmailValidator = new EmailValidator();
			
			public function set aktivBruker(bruker:Bruker):void
			{
				_aktivBruker = bruker;
				tilbakestillForm();
				
				if(Components.instance.session.bruker.epost == _aktivBruker.epost)
				{
					superAdminFelt.visible = false;
					superAdminFelt.includeInLayout = false;
					superAdminFelt.enabled = false;
				}
			}
			
			private function init():void
			{	
				settValidatorer();
				tilbakestillForm();
			}
			
			private function formVerdier():Bruker
			{
				var bruker:Bruker = new Bruker();
				
				bruker.epost = epost.text;
				bruker.fornavn = fornavn.text;
				bruker.etternavn = etternavn.text;
				bruker.passord = passord.text;
				bruker.superadmin = superadmin.selected;
				
				return bruker;
			}
			
			private function passordSjekk():Boolean
			{
				if(StringUtil.trim(passord.text) == StringUtil.trim(passord2.text))
				{
					return true;
				}
				else
				{
					Alert.show("Passordet er ikke lik passordbekreftelsen\nPrøv igjen", "Passordfeil");
					passord.text = "";
					passord2.text = "";
					return false;
				}	
			}
			
			private function sammenliknObjekt(objekt1:Object,objekt2:Object):Boolean
			{
			    var buffer1:ByteArray = new ByteArray();
			    buffer1.writeObject(objekt1);
			    var buffer2:ByteArray = new ByteArray();
			    buffer2.writeObject(objekt2);
			 	
			    var storrelse:uint = buffer1.length;
			    if (buffer1.length == buffer2.length)
			    {
			        buffer1.position = 0;
			        buffer2.position = 0;
			 
			        while (buffer1.position < storrelse)
			        {
			            var v1:int = buffer1.readByte();
			            if (v1 != buffer2.readByte())
			            {
			                return false;
			            }
			        }    
			        return true;                        
			    }
			    return false;
			}
			
			private function lagre():void
			{
				if(passordSjekk())
				{
					if(_aktivBruker)
					{
						if(Components.instance.session.bruker.epost == _aktivBruker.epost)
						{
							Components.instance.controller.redigerEgenBruker(Components.instance.session.bruker, formVerdier());
						}
						else
						{
							Components.instance.controller.redigerBruker(_aktivBruker, formVerdier());
						}	
					}
					else
					{
						Components.instance.controller.leggInnBruker(formVerdier());
					}
				}
			}
			
			private function tilbakestillForm():void
			{
				if(_aktivBruker)
				{
					epost.text = _aktivBruker.epost;
					fornavn.text = _aktivBruker.fornavn;
					etternavn.text = _aktivBruker.etternavn;
					superadmin.selected = _aktivBruker.superadmin;
				}
				else
				{
					epost.text = "";
					fornavn.text = "";
					etternavn.text = "";
					superadmin.selected = false;
				}
				
				passord.text = "";
				passord2.text = "";
				
				erTilbakestillt = sammenliknObjekt(formVerdier(), _aktivBruker);
			}
			
			private function avbryt():void
			{
				Components.instance.controller.fjernBrukerVindu();
			}
			
			private function validering(event:Event):void 
            {                    
                hvilkeKontroll = event.target as DisplayObject;
                var likTest:Boolean = sammenliknObjekt(formVerdier(), _aktivBruker);
                
                if((_aktivBruker != null) && !likTest)
             	{
             		erTilbakestillt = false;
             	}
             	
             	if(likTest)
             	{
             		erTilbakestillt = true;
             	}
             	
             	erGodkjent = true;

                valider(epostValidator);                
            }
            
			private function valider(validerer:Validator):Boolean
            {                
	            var validatoren:DisplayObject = validerer.source as DisplayObject;
	            var ikkeVisBoks:Boolean = (validatoren != hvilkeKontroll);
	            var event:ValidationResultEvent = validerer.validate(null, ikkeVisBoks); 
	            var erValidert:Boolean = (event.type == ValidationResultEvent.VALID);
	            
	            erGodkjent = erGodkjent && erValidert;
	             
	            return erValidert;
             } 
             
            private function settValidatorer():void
            {             	
             	epostValidator.source = epost;
             	epostValidator.property = "text";
             	epostValidator.missingAtSignError = "Mangler @ i epost";
             	epostValidator.invalidCharError = "Ulovlig tegn i epost";
             	epostValidator.invalidPeriodsInDomainError = "Du har for mange punktummer i domene navnet ditt";
             	epostValidator.invalidDomainError = "Det er noe feil i domenet ditt";
             	epostValidator.missingPeriodInDomainError = "Mangler punktum i domenet ditt";
             	epostValidator.missingUsernameError = "Du har glemt navnet før @";
             	epostValidator.tooManyAtSignsError = "Du har for mange @ i eposten din";
			 }
		]]>
	</mx:Script>
	
	<mx:VBox horizontalAlign="center">
		<mx:Form paddingBottom="0" paddingTop="0" paddingLeft="0" paddingRight="0">
			<mx:FormItem label="E-post">
				<mx:TextInput id="epost" change="validering(event)"/>
			</mx:FormItem>
			<mx:FormItem label="Fornavn">
				<mx:TextInput id="fornavn" change="validering(event)"/>
			</mx:FormItem>
			<mx:FormItem label="Etternavn">
				<mx:TextInput id="etternavn" change="validering(event)"/>
			</mx:FormItem>
			<mx:FormItem label="Passord">
				<mx:TextInput id="passord" displayAsPassword="true"/>
			</mx:FormItem>
			<mx:FormItem label="Bekreft passord">
				<mx:TextInput id="passord2" displayAsPassword="true"/>
			</mx:FormItem>
			<mx:FormItem label="Superadmin" id="superAdminFelt">
				<mx:CheckBox id="superadmin" change="validering(event)"/>
			</mx:FormItem>
		</mx:Form>
	</mx:VBox>
	
	<mx:ControlBar horizontalAlign="center">
        <mx:Button label="Lagre" enabled="{!erTilbakestillt}" click="lagre()"/>
        <mx:Button label="Tilbakestill" enabled="{!erTilbakestillt}" click="tilbakestillForm()"/>
        <mx:Button label="Avbryt" click="avbryt()"/>
	</mx:ControlBar>
</mx:TitleWindow>